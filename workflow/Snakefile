import pandas as pd
import pysam
from snakemake.utils import min_version
min_version("5.30.1")

configfile: "config/config.yaml"

INPUT_DIR = config["input_dir"]
OUTPUT_DIR = config["output_dir"]
THREADS = config["threads"]
samples, = glob_wildcards(INPUT_DIR + "/{sample}.bam")

pooling_mode = "PR" if config["pooled"] else "R"

rule all:
    input:
        OUTPUT_DIR+"/overview.tsv",
        expand("{out}/{p}/{sample}.{p}.gz", sample=samples, out=OUTPUT_DIR, p=pooling_mode)


include: "rules/junctions.smk"
include: "rules/sites.smk"
include: "rules/genome.smk"
include: "rules/pooled_sites.smk"



rule describe_replicates:
    input:
        filtered_junctions=expand("{out}/J6/{sample}.J6.gz", out=OUTPUT_DIR, sample=samples),
        stats=expand("{out}/J1/{sample}.stats", out=OUTPUT_DIR, sample=samples)
    output:
        tsv=OUTPUT_DIR+"/overview.tsv"
    shell:
        "python3 -m workflow.scripts.describe_replicates "
        "{input.stats} "
        "-o {output.tsv}"


rule compute_rates:
    input:
        filtered_junctions=rules.filter_junctions.output.filtered_junctions,
        filtered_sites=rules.filter_sites.output.filtered_sites
    output:
        rates=OUTPUT_DIR+"/R/{sample}.R.gz"
    shell:
         "python3 -m workflow.scripts.compute_rates "
         "-j {input.filtered_junctions} "
         "-s {input.filtered_sites} "
         "-o {output.rates}"


rule compute_pooled_rates:
    input:
        filtered_junctions=rules.filter_junctions.output.filtered_junctions,
        filtered_pooled_sites=rules.filter_pooled_sites.output.filtered_pooled_sites
    output:
        rates=OUTPUT_DIR+"/PR/{sample}.PR.gz"
    shell:
         "python3 -m workflow.scripts.compute_rates "
         "-j {input.filtered_junctions} "
         "-s {input.filtered_pooled_sites} "
         "-o {output.rates}"
